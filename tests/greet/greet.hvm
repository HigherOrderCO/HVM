// Like echo, but with sharing

Main =
    (PutStrLn "What is your name?"
    (ReadLine @l
    (PutStrLn (Append "Hello, " l)
    (PutStrLn (Append "Goodbye, " l)
    (Console.done ThatsAll
    )))))

// IO utils
// ////////

// (PutStrLn str x) wraps x in IO requests that print str
(PutStrLn (String.cons chr str) cont) = (Console.put_char chr (PutStrLn str cont))
(PutStrLn (String.nil) cont) = (Console.put_char 10 cont)

(ReadLine cont) = (Console.get_char (ReadLineHelper_1 cont))

(ReadLineHelper_1 cont) = @c (ReadLineHelper_2 cont c)
(ReadLineHelper_2 cont 10) = (cont String.nil)
(ReadLineHelper_2 cont c)  = (Console.get_char (ReadLineHelper_1 @s (cont (String.cons c s))))

// General utils
// /////////////

// (RevApp s1 s2) reverses string s1, and appends s2
(RevApp String.nil s2) = s2
(RevApp (String.cons c s1) s2) = (RevApp s1 (String.cons c s2))

(Reverse s) = (RevApp s String.nil)

(Append s1 s2) = (RevApp (Reverse s1) s2)
